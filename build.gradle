/*
 *  This file is part of MinePass, licensed under the MIT License (MIT).
 *
 *  Copyright (c) MinePass.net <http://www.minepass.net>
 *
 *  Permission is hereby granted, free of charge, to any person obtaining a copy
 *  of this software and associated documentation files (the "Software"), to deal
 *  in the Software without restriction, including without limitation the rights
 *  to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 *  copies of the Software, and to permit persons to whom the Software is
 *  furnished to do so, subject to the following conditions:
 *
 *  The above copyright notice and this permission notice shall be included in
 *  all copies or substantial portions of the Software.
 *
 *  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 *  IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 *  FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 *  AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 *  LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 *  OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 *  THE SOFTWARE.
 */

loadMinetestConfiguration()

def loadMinetestConfiguration() {
    project.ext['minetestEnv'] = hasProperty('env') ? env : 'development'
    println "Minetest Environment is set to ${project.minetestEnv}"

    project.ext.coreVersion = '2.0-SNAPSHOT'
    println "Minetest MP-Core Version is set to ${project.coreVersion}"
}

import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'java'
apply plugin: 'application'


sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

version = "0.2.2" + "-" + (System.getenv("BUILD_NUMBER") ?: "SNAPSHOT")
group = "net.minepass"
archivesBaseName = "MinePass-MinetestWrapper"
mainClassName = "net.minepass.gs.mt.wrapper.MP_MinetestWrapper"

processResources {
    filter ReplaceTokens, tokens:[VERSION: project.version]
}

configurations {
    minepass_core
    compile {
        extendsFrom minepass_core
    }
}

dependencies {
    if (project.minetestEnv == 'core_dev') {
        minepass_core(project(":minepass_core")) {
            transitive = true
        }
    } else if (project.minetestEnv == 'development') {
        minepass_core group: 'net.minepass', name: 'minepass-gameserver', version: "${project.coreVersion}", changing: true
    } else if (project.minetestEnv == 'production') {
        minepass_core files("minepass-gameserver-${project.coreVersion}.jar")
    }
}

jar {
    manifest {
        attributes 'Main-Class': 'net.minepass.gs.mt.wrapper.MP_MinetestWrapper'
    }
}

task extractCore(type: Copy) {
    from {  // use of closure defers evaluation until execution time
        configurations.minepass_core.collect { zipTree(it) }
    }
    into "$buildDir/classes/main/"
}

classes.dependsOn extractCore

task zip(type: Zip) {
    from jar.outputs.files
    from('mod/') {
        into('mod')
    }
    from('README.md')
    from("LICENSE.txt")
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
}

